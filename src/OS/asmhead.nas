;haribote-os boot asm
;TAB=4

BOTPAK	EQU 0X00280000
DSKCAC	EQU 0X00100000
DSKCAC0 EQU 0X00008000

;有关BOOT_INFO
CYLS	EQU 0X0FF0	;设定启动区
LEDS 	EQU 0X0FF1
VMODE	EQU 0X0FF2	;关于颜色数目的信息
SCRNX	EQU 0X0FF4	;分辨率的X
SCRNY	EQU 0X0FF6	;分辨率的Y
VRAM	EQU 0X0FF8 	;图像缓冲区的开始位置

;编译后的文件放在0X4200处，在内存的位置为0X8000+0X4200=0XC200
	ORG 0XC200	;程序要装载的内存位置
	MOV AL, 0X13	;vga显卡， 320×200×8位彩色
	MOV AH, 0X00
	INT 0X10
	
	MOV BYTE [VMODE], 8	;记录画面模式
	MOV WORD [SCRNX], 320
	MOV WORD [SCRNY], 200
	MOV DWORD [VRAM], 0X000A0000;VRAM从0xa000-0xafff的64K

;用bios取得键盘上各种LED指示灯的状态
	MOV AH, 0X02
	INT 0X16	;keyboard bios
	MOV [LEDS], AL

;
	MOV AL, 0XFF
	OUT 0X21, AL
	NOP
	OUT 0XA1, AL
	CLI

;
	CALL WAITKBDOUT
	MOV AL, 0XD1
	OUT 0X64, AL
	CALL WAITKBDOUT
	MOV AL, 0XDF	;ENABLE A20
	OUT 0X60, AL
	CALL WAITKBDOUT

;
[INSTRSET "i486p"]
	  LGDT [GDTR0]
	  MOV EAX, CR0
	  AND EAX, 0X7FFFFFFF
	  OR  EAX, 0X00000001
	  MOV CR0, EAX
	  JMP PIPELINEFLUSH

PIPELINEFLUSH:
	MOV AX, 1*8
	MOV DS, AX
	MOV ES, AX
	MOV FS, AX
	MOV GS, AX
	MOV SS, AX
;
	MOV ESI, bootpack
	MOV EDI, BOTPAK
	MOV ECX, 512*1024/4
	CALL memcpy

;
	MOV ESI, 0X7C00
	MOV EDI, DSKCAC
	MOV ECX, 512/4
	CALL memcpy
;
	MOV ESI, DSKCAC0+512
	MOV EDI, DSKCAC+512
	MOV ECX, 0
	MOV CL, BYTE [CYLS]
	IMUL ECX, 512*18*2/4
	SUB ECX, 512/4
	CALL memcpy

;asmhead
;bootpack
	MOV EBX, BOTPAK
	MOV ECX, [EBX+16]
	ADD ECX, 3		;ECX += 3
	SHR ECX, 2		;ECX /=4
	JZ  SKIP
	MOV ESI, [EBX+20]
	ADD ESI, EBX
	MOV EDI, [EBX+12]
	CALL 	 memcpy

SKIP:
	MOV ESP, [EBX+12]
	JMP DWORD 2*8:0X0000001B

WAITKBDOUT:
	IN AL, 0X64
	AND AL, 0X02
	JNZ WAITKBDOUT
	RET

memcpy:
	MOV EAX, [ESI]
	ADD ESI, 4
	MOV [EDI], EAX
	ADD EDI, 4
	SUB ECX, 1
	JNZ memcpy
	RET

	ALIGNB 16
GDT0:
	RESB 8
	DW   0XFFFF, 0X0000, 0X9200, 0X00CF
	DW   0XFFFF, 0X0000, 0X9A28, 0X0047
	DW   0
GDTR0:
	DW 8*3-1
	DD GDT0
	
	ALIGNB	16
bootpack: